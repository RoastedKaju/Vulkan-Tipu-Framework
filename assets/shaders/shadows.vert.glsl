#version 460
#extension GL_EXT_buffer_reference: require
#extension GL_EXT_scalar_block_layout: require
#extension GL_EXT_shader_explicit_arithmetic_types_int64: require

layout (push_constant) uniform PushConstants {
    mat4 model;
    uint64_t address;
    uint albedo;
};

struct ShaderData {
    mat4 projection;
    mat4 view;
    mat4 lightSpaceMatrix;
    vec3 lightDirection;
    uint shadowMap;
};

layout (buffer_reference, scalar) readonly buffer ShaderDataRef {
    ShaderData data;
};

layout (location = 0) in vec3 inPosition;
layout (location = 1) in vec3 inNormal;
layout (location = 2) in vec2 inUV;
layout (location = 3) in vec4 inTangent;

layout (location = 0) out vec3 outNormal;
layout (location = 1) out vec2 outUV;
layout (location = 2) out flat uint outAlbedo;
layout (location = 3) out vec3 outLightDirView;
layout (location = 4) out vec3 outFragPosView;
layout (location = 5) out vec4 outFragPosLightSpace;
layout (location = 6) out flat uint outShadowMap;

void main() {
    ShaderData data = ShaderDataRef(address).data;

    mat4 mv = data.view * model;
    vec4 worldPos = model * vec4(inPosition, 1.0);

    gl_Position = data.projection * mv * vec4(inPosition, 1.0);

    outNormal = normalize(mat3(mv) * inNormal);

    outFragPosView = vec3(mv * vec4(inPosition, 1.0));

    outLightDirView = normalize(mat3(data.view) * data.lightDirection);

    outFragPosLightSpace = data.lightSpaceMatrix * worldPos;
    outShadowMap = data.shadowMap;

    outUV = inUV;
    outAlbedo = albedo;
}